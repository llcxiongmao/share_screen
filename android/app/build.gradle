import java.util.regex.Matcher
import java.util.regex.Pattern

plugins {
    id 'com.android.application'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "org.llc.share_screen"
        minSdk 26
        targetSdk 34

        // get version name and code from version.txt.
        String versionFileName = projectDir.getParentFile().getParentFile().toString() + "/version.txt"
        try(BufferedReader br = new BufferedReader(new FileReader(versionFileName))) {
            String versionString = br.readLine()
            Pattern p = Pattern.compile("name=(\\d+\\.\\d+\\.\\d+) code=(\\d+)")
            Matcher m = p.matcher(versionString)
            if (!m.matches()) {
                throw new RuntimeException("bad version string: " + versionString)
            }
            versionName m.group(1)
            versionCode Integer.parseInt(m.group(2))
        }

        var nowT = new Date().format('yyyyMMddHHmm').toString()
        buildConfigField "String", "SS_BUILD_TIME", "\"${nowT}\""
        archivesBaseName = "share_screen(${versionName})_${nowT}"
    }

    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword '111111'
            storeFile file('1.jks')
            storePassword '111111'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release_with_debug {
            initWith(buildTypes.release)
            applicationIdSuffix ".release_with_debug"
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        disable 'DefaultLocale'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}